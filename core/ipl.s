;准备进入保护模式
;设定GDT
;GDT保存空间范围：
VIFO	EQU		0X0200
VMODE	EQU		0X0
SCRENX	EQU		0X2
SCRENY	EQU     0X4
VRAM	EQU		0X8


VBEMODE EQU		0X105
;----------------------------------------------
START_OF_SYSTEM 	EQU		3
END_OF_SYSTEM		EQU		4
ORG		0X7E00
MOV		AX,0X7C00
MOV		SS,AX
MOV		SP,0X7C00
;----------------------------------------------
;切换到图形显示模式  
;测试VBE是否存在
MOV		AX,0X9000
MOV		ES,AX
MOV		DI,0


MOV		AX,0X4F00
INT		0X10
CMP		AX,0X004F
JNE		SCRN320

;如果VBE存在，检查版本


MOV		AX,[ES:DI+4]
CMP		AX,0x0200
JB		SCRN320	


MOV		CX,VBEMODE
MOV		AX,0x4f01
INT		0x10
CMP		AX,0x004f
JNE		SCRN320
		
		
CMP		BYTE [ES:DI+0x19],8
JNE		SCRN320
CMP		BYTE [ES:DI+0x1b],4
JNE		SCRN320
MOV		AX,[ES:DI+0x00]
AND		AX,0x0080
JZ		SCRN320



;VBE正确，使用高分辨率



MOV		BX,0X4105
MOV		AX,0X4F02
INT 	0X10
MOV		AX,VIFO
MOV		GS,AX
MOV		AX,[ES:DI+0X12]
MOV		[GS:SCRENX],AX
MOV		AX,[ES:DI+0X14]
MOV		[GS:SCRENY],AX
MOV		BYTE [GS:VMODE],8
MOV		EAX,[ES:DI+0X28]
MOV		[GS:VRAM],EAX
JMP		GDT_SET


SCRN320:
MOV		AH,0X00
MOV		AL,0X13
INT		0X10
MOV		AX,VIFO
MOV		GS,AX
MOV		WORD[GS:SCRENX],320
MOV		WORD[GS:SCRENY],200
MOV		BYTE [GS:VMODE],8
MOV		DWORD[GS:VRAM],0X000A0000
;---------------------------------------------
;初始化GDT
GDT_SET:
MOV		AX,[CS:GDT_ADRESS]
MOV		DX,[CS:GDT_ADRESS+0X02]
MOV		BX,16
DIV		BX
MOV		DS,AX
MOV		BX,DX
;#0  NULL
MOV		DWORD		[BX+0X00],0X00000000
MOV		DWORD		[BX+0X04],0X00000000
;#1  MBR IPL CODE 
MOV		DWORD		[BX+0X08],0X7c0003ff
MOV		DWORD		[BX+0X0C],0X00409800
;#2  GDT DATA
MOV		DWORD		[BX+0X10],0X8000ffff
MOV		DWORD		[BX+0X14],0X00409200   
;#3  CORE CODE 
MOV		DWORD		[BX+0X18],0X0000ffff
MOV		DWORD		[BX+0X1c],0X004d9a02
;#4  CORE DATA
;MOV		DWORD		[BX+0X20],0X0000ffff
;MOV		DWORD		[BX+0X24],0X004f9210
;#4  CORE STACK
MOV		DWORD		[BX+0X20],0X7c00fffb
MOV		DWORD		[BX+0X24],0X00cf9600
;#5  GLOBAL DATA 4G
MOV		DWORD		[BX+0X28],0x0000FFFF
MOV		DWORD		[BX+0X2C],0x00CF9200
;#6	 CORE CODE DATA
;MOV		DWORD		[BX+0X30],0X0000ffff
;MOV		DWORD		[BX+0X34],0X00409802


MOV		WORD [CS:GDT_SIZE],0xffff
LGDT	[CS:GDT_SIZE]
;---------------------------------------------
;初始化IDT
MOV		WORD [CS:IDT_SIZE],0x7ff
LIDT	[CS:IDT_SIZE]
;打开A20
A20:
IN 		AL,0X92
OR 		AL,0X02
OUT		0X92,AL
;不屏蔽中断会造成屏幕闪烁
CLI


;保护模式开关设置为1
MOV		EAX,CR0
OR		EAX,1
MOV		CR0,EAX
;清空流水线
JMP		DWORD	0X0008:(PROTECT_MODE_ENTRANCE-0x7c00)
;---------------------------------------------
[bits 32]
PROTECT_MODE_ENTRANCE:
;栈段初始化

MOV		AX,0X20
MOV		SS,AX
MOV		ESP,0X00

;寄存器初始化
MOV		AX,0X28
MOV		DS,AX
MOV		ES,AX
MOV		FS,AX
MOV		GS,AX
XOR 	AX,AX
MOV		DI,AX
XOR		EAX,EAX
XOR		EBX,EBX
XOR		ECX,ECX
XOR		EDX,EDX

;---------------------------------------------
;c语言模式
GO_TO_SYSTEM_CORE:
JMP		DWORD	0x0018:0X0000

;GDT存储位置
GDT_SIZE	DW		0 
GDT_ADRESS	DD		0x00008000
IDT_SIZE	DW		0
IDT_ADRESS	DD		0X00018000
TIMES	512-($-$$) DB	0
